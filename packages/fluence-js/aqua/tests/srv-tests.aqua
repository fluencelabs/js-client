module Export

import Srv from "../src/single-module-srv.aqua"
export happy_path, list_services, file_not_found, service_removed, removing_non_exiting

service Greeting("greeting"):
    greeting(name: string) -> string

func happy_path(file_path: string) -> string:
    wasm <- Srv.read_file(file_path)
    id <- Srv.create(wasm)
    Greeting id
    <- Greeting.greeting("test")

func list_services(file_path: string) -> []string:
    wasm <- Srv.read_file(file_path)
    Srv.create(wasm)
    Srv.create(wasm)
    Srv.create(wasm)
    <- Srv.list()

func file_not_found() -> string:
    try:
        wasm <- Srv.read_file("/random/incorrect/file")
    catch e:
        <- e.message

func service_removed(file_path: string) -> string:
    wasm <- Srv.read_file(file_path)
    id <- Srv.create(wasm)
    Greeting id
    Srv.remove(id)
    try:
        dontcare <- Greeting.greeting("test")
    catch e: 
        <- e.message

func removing_non_exiting() -> string:
    try:
        Srv.remove("random_id")
    catch e: 
        <- e.message
    
